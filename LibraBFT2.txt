type BlockId
type Block = struct {
    parent: BlockId,
    round: uint,
}

function Hash(Block): BlockId
const blocks: [BlockId]Block 
axiom forall id: BlockId. Hash(blocks[id]) == id

const h: uint // number of honest replicas
const f: uint // number of faulty replicas
axiom h > 2f

const root: BlockId // root of block tree
axiom blocks[root].round == 0 && blocks[root].parent == root

type HonestReplicaId = 1..h // ids of honest replicas
type HonestQuorum = {q: Set<HonestReplicaId> | size(q) >= h - f} // all possible quorums of honest nodes

// primitive function that returns true iff id' can reach id by following parent link 0 or more times
function Reaches(id': BlockId, id: BlockId): bool;

function Consistent(id': BlockId, id: BlockId) : bool {
    Reaches(id', id) || Reaches(id, id')
}

//// global model variables

// all collected votes sent by honest replicas
var voteStore: [BlockId]Set<HonestReplicaId>

// id of the latest globally-committed block
var globallyCommitted: BlockId

//// per-replica variables

// all blocks on which quorum has been established
var qcStore: Set<BlockId>

// monotonically increasing round of the last voted block
var lastVoteRound: uint

// monotonically increasing round of the locked block
var lockedBlockRound: uint

//// procedures

procedure Initialize()
{
    voteStore := (lambda id: BlockId. if id = root then HonestReplicaId else {})
    globallyCommitted := root
    qcStore := (lambda r: HonestQuorum. { root })
    lastVoteRound := (lambda r: HonestReplicaId. 0)
    lockedBlockRound := (lambda r: HonestReplicaId. 0)
}

//// start things off
procedure Main() {
    var r: HonestReplicaId
    var blockId: BlockId

    // propose or commit nondeterministically
    if * {
        async OnProposal(r, blockId)
    } 
    else if * {
        async OnQuorumCertificate(r, blockId)
    } 
    else {
        async OnCommit(r, blockId)
    }

    // keep going nondeterministically
    if * {
        async Main()
    }
}

//// event handler at a replica to "send" vote and update lastVoteRound
procedure OnProposal(r: HonestReplicaId, newBlockId: BlockId)
reads  qcStore, voteStore, lastVoteRound
writes voteStore, lastVoteRound
{
    var newBlock, b'': Block
    var id'': BlockId

    newBlock := blocks[newBlockId]
    id'' := newBlock.parent
    if !(id'' in qcStore[r]) {
        return
    }
    
    // possibly vote
    b'' := blocks[id'']
    if newBlock.round > lastVoteRound[r] && b''.round >= lockedBlockRound[r] {
        lastVoteRound[r] := newBlock.round
        voteStore[newBlockId] := voteStore[newBlockId] + r
    }
}

procedure TryAddQuorumCertificate(blockId: BlockId) : bool 
reads  qcStore, voteStore
writes qcStore
{
    var numEquivocators: uint

    // numEquivocators is the number of faulty replicas who voted for blockId
    assume numEquivocators <= f
    if size(voteStore[blockId]) + numEquivocators >= h {
        qcStore[r] := qcStore[r] + blockId
    }
    blockId in qcStore[r]
}

//// event handler at a replica to add a quorum certificate and update lockedBlockRound
procedure OnQuorumCertificate(r: HonestReplicaId, id'': BlockId)
reads  qcStore, voteStore, lockedBlockRound
writes qcStore, lockedBlockRound
{
    var b'', b': Block

    if !TryAddQuorumCertificate(id'') {
        return
    }

    // possibly update locked block
    b'' := blocks[id'']
    b'  := blocks[b''.parent]
    if b'.round > lockedBlockRound[r] {
        lockedBlockRound[r] := b'.round
    }
}

//// event handler at a replica to commit a block and assert consensus safety
procedure OnCommit(r: HonestReplicaId, id'': BlockId)
reads  qcStore, globallyCommitted
writes globallyCommitted
{
    var id', id: BlockId
    var b'', b', b: Block

    if !(id'' in qcStore[r]) {
        return
    }
    b'' := blocks[id'']
    id' := b''.parent
    b'  := blocks[id']
    id  := b'.parent
    b := blocks[id]
    if b''.round == b'.round + 1 && b'.round == b.round + 1 {
        assert Consistent(id, globallyCommitted)
        if Reaches(id, globallyCommitted) {
            globallyCommitted := id
        }
    }
}

////  Invariants

function OneChain(id: BlockId, id': BlockId): bool
{
    id = blocks[id'].parent
}

function TwoChain(id: BlockId, id': BlockId, id'': BlockId) : bool 
{
    OneChain(id, id') && OneChain(id', id'')
}

function ContiguousTwoChain(id: BlockId, id': BlockId, id'': BlockId) : bool 
{
    TwoChain(id, id', id'') && 
    blocks[id].round + 1 == blocks[id'].round &&
    blocks[id'].round + 1 == blocks[id''].round
}

forall r in HonestReplicaId. id, id' in qcStore[r]. 
    OneChain(id, id') ==> (id' == root || blocks[id].round < blocks[id'].round)

forall r in HonestReplicaId. id, id' in qcStore[r]. 
    blocks[id].round == blocks[id'].round ==> id == id'

forall r in HonestReplicaId. id, id', id'' in qcStore[r].
    TwoChain(id, id', id'') ==>
        (exists q: HonestQuorum. (forall x: HonestReplicaId. x in q ==> lockedBlockRound[x] >= blocks[id].round))

forall r in HonestReplicaId. id, id', id'' in qcStore[r].
    ContiguousTwoChain(id, id', id'') ==>
        (forall x in qcStore[r]. blocks[x].round > blocks[id''].round ==> Reaches(x, id))

forall r in HonestReplicaId. xid, xid', xid'', yid, yid', yid'' in qcStore[r].
    ContiguousTwoChain(xid, xid', xid'') && ContiguousTwoChain(yid, yid', yid'') ==> Consistent(xid, yid)
