type BlockId
type Block = struct {
    parent: BlockId,
    height: uint,
    justify: BlockId,
}

function Hash(Block): BlockId
const blocks: [BlockId]Block 
axiom forall id: BlockId. Hash(blocks[id]) == id

const h: uint // number of honest replicas
const f: uint // number of faulty replicas
axiom h > 2f + 1

const root: BlockId // root of block tree
axiom blocks[root].height == 0 && blocks[root].parent == root && blocks[root].justify == root

type HonestReplicaId = 1..h // ids of honest replicas

//// global model variables

// all collected votes 
// initial value: lambda id: BlockId. if id == root then h+f else f (maximum equivocation)
var voteStore: [BlockId]uint

// id of the latest globally-committed block 
// initial value: root  
var committed: BlockId

//// per-replica state

// monotonically increasing height of the last voted block 
// initial value: 0
var vheight: uint

// id of the locked block
// initial value: root
var lockedBlockId: BlockId

// id of the latest locally-committed block 
// initial value: root
var locallyCommitted: BlockId

// primitive function that evaluates as follows: 
// if id' can reach id by following parent link 0 or more times, then set of all nodes along the path (id and id' inclusive)
// otherwise, empty set 
function Between(id', id) : [BlockId]bool

function Reachable(id', id): bool {
    Between(id', id) != {}
}

function extends(newProposalId: BlockId, lockedBlockId: BlockId) : bool {
    Reachable(newProposalId, lockedBlockId) && 
    (forall id in Between(newProposalId, lockedBlockId). 
        id == lockedBlockId || blocks[id].height == blocks[blocks[id].parent].height + 1)
}

function consistent(id': BlockId, id: BlockId) : bool {
    Reachable(id', id) || Reachable(id, id')
}

//// start things off
procedure main() {
    var r: HonestReplicaId
    var newBlockId: BlockId

    async OnReceiveProposal(r, newBlockId)
    if * {
        async Main()
    }
}

//// top-level event handler at a replica to update vheight and "send" vote
procedure OnReceiveProposal(r: HonestReplicaId, newBlockId: BlockId) {
    var newBlock: Block

    newBlock := blocks[newBlockId]
    if * || voteStore[newBlock.justify] < h {
        return
    }
    if newBlock.height > vheight[r] &&
            (extends(newBlockId, lockedBlockId[r]) ||
             blocks[newBlock.justify].height > blocks[lockedBlockId[r]].height) {
        vheight[r] := newBlock.height
        voteStore[newBlockId] := voteStore[newBlockId] + 1
    }
    async Update(r, newBlock.justify)
}

//// internal event handler at a replica to update lockedBlockId, locallyCommitted, and committed
//// and assert consensus safety
procedure Update(r: HonestReplicaId, id'': BlockId) {
    var id', id: BlockId
    var b'', b', b: Block

    b'' := blocks[id'']
    id' := b''.justify
    b'  := blocks[id']
    id  := b'.justify
    b   := blocks[id]
    if b'.height > blocks[lockedBlockId[r]].height {
        lockedBlockId[r] := id'
    }
    if b''.parent == id' && b'.parent == id {
        assert consistent(id, committed)
        if Reachable(id, committed) {
            locallyCommitted[r] := id
            committed := id
        }
    }
}
